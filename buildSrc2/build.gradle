apply {
	plugin 'java'
	plugin 'groovy'
	plugin 'tomcat'
}

buildscript {
	repositories {
		add(new org.apache.ivy.plugins.resolver.URLResolver()) {
			name = 'GitHub'
			addArtifactPattern 'http://cloud.github.com/downloads/[organisation]/[module]/[module]-[revision].[ext]'
		}
	}
	dependencies {
		classpath 'bmuschko:gradle-tomcat-plugin:0.8.2'
	}
}

sourceCompatibility = 1.6

logger.quiet "Building the Gripes Plugin"

group = 'gripes'
archivesBaseName = 'gripes-plugin'
version = '0.1.10'
libsDirName = "libs/${group}/${archivesBaseName}/${version}"

versions = [
	'gripes-web' : '0.1.9',
	'gripes-plugin' : '0.1.10',
	'gripersist' : '0.1.6',
	'hibernate-core' : '4.0.0.Beta2',
	'tomcat' : '6.0.35'
]

repositories {
    mavenCentral()
    mavenRepo url: "http://www.gripes-project.org/libs"
    mavenRepo name: "jboss-snapshots", url: "http://snapshots.jboss.org/maven2/"
    mavenRepo name: "jboss-public", url: "https://repository.jboss.org/nexus/content/groups/public/"
	
	add(new org.apache.ivy.plugins.resolver.URLResolver()) {
		name = 'GitHub'
		addArtifactPattern 'http://cloud.github.com/downloads/[organisation]/[module]/[module]-[revision].[ext]'
	}
}

sourceSets {
	main {
		groovy { srcDir 'src' }
	    resources { srcDir 'resources' }
	}	
	test {
		groovy { srcDir 'test' }
	    resources { srcDir 'test' }
	}	
}

configurations {
	ftpAntTask {}
	provided {}
}

dependencies {	
	//** Gradle API
	compile gradleApi()
	
	//** Ant FTP
	ftpAntTask module("org.apache.ant:ant-commons-net:1.7.0") {
        module("commons-net:commons-net:1.4.1") {
            dependencies("oro:oro:2.0.8@jar")
        }
    }
    
	//** Groovy
	groovy group: 'org.codehaus.groovy', name: 'groovy', version: '1.8.4'
	

	//** Framework
	compile group: 'net.sourceforge.stripes', name: 'stripes', version: '1.5.6'
	
	//** Database
	compile group: 'org.hsqldb', name: 'hsqldb', version: '2.0.0'
	
	//** Hibernate
	compile group: 'org.hibernate', name: 'hibernate-annotations', version: '3.5.6-Final'
	compile group: 'org.hibernate', name: 'hibernate-core', version: versions['hibernate-core']
	compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: versions['hibernate-core']
	runtime group: 'org.hibernate', name: 'hibernate-c3p0', version: versions['hibernate-core']
	
	compile group: 'javax.persistence', name: 'persistence-api', version: '1.0'
	
	//** Jetty
	compile group: 'org.mortbay.jetty', name: 'jetty', version: '6.1.26'
	
//	tomcat 	"org.apache.tomcat:catalina:${versions['tomcat']}",
//			"org.apache.tomcat:coyote:${versions['tomcat']}",
//			"org.apache.tomcat:jasper:${versions['tomcat']}"
	compile 'bmuschko:gradle-tomcat-plugin:0.8.2'

	providedCompile group: 'javax.servlet', name: 'jstl', version: '1.2'
	providedCompile group: 'taglibs', name:'standard', version: '1.1.2'	
	
	compile group: 'gripes', name: 'gripersist', version: versions['gripersist']
	compile group: 'gripes', name: 'gripes-web', version: versions['gripes-web']
	
	testCompile 'junit:junit:4.7'
	
	//** For archiving
	archives group: 'org.hsqldb', name: 'hsqldb', version: '2.0.0'	
	archives group: 'org.hibernate', name: 'hibernate-annotations', version: '3.5.6-Final'
	archives group: 'org.hibernate', name: 'hibernate-core', version: versions['hibernate-core']
	archives group: 'org.hibernate', name: 'hibernate-entitymanager', version: versions['hibernate-core']
	archives group: 'org.hibernate', name: 'hibernate-c3p0', version: versions['hibernate-core']
	archives group: 'javax.persistence', name: 'persistence-api', version: '1.0'
	archives group: 'gripes', name:'gripes-web', version: '0.1.7'
}

jar.enabled = true
uploadArchives.setDependsOn([jar])
uploadArchives {
    repositories {
       flatDir(dirs: file('repos'))
    }
}
uploadArchives.doLast {
	ant {
		taskdef(name: 'ftp',
                classname: 'org.apache.tools.ant.taskdefs.optional.net.FTP',
                classpath: configurations.ftpAntTask.asPath)
		
		ftp(action: "mkdir",
			server: "www.gripes-project.org", 
			remotedir: "${ftpUploadRoot}/libs/gripes/gripes-plugin/${version}",
			chmod: "0755",
			userid: ftpUploadUser,
			password: ftpUploadPassword)
		
		ftp(action: "put",
			server: "www.gripes-project.org", 
			remotedir: "${ftpUploadRoot}/libs/gripes/gripes-plugin/${version}",
			chmod: "0755",
			userid: ftpUploadUser,
			password: ftpUploadPassword) {
			fileset(dir:"build/libs/gripes/gripes-plugin/${version}", includes: "*${version}.*")
		}
    }
}